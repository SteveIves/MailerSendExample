
import System.Text
import System.Text.Json

function MailerSend, boolean
    required in SenderName, string
    required in SenderAddress, string
    required in RecipientNames, [#]string
    required in RecipientAddresses, [#]string
    required in Subject, string
    required in BodyText, string
    required out ErrorMessage, string

    stack record
        ok, boolean
        requestBody, string
        apiToken, string
    endrecord

proc
    ok = true
    ErrorMessage = ""

    ;TODO: Validate parameters


    ;Get the API token from MailerSend.settings.json
    if (ok)
    begin
        data tmpch, i4
        data tmpbuf, a80
        try
        begin
            open(tmpch=0,i:s,"MailerSendApiToken.txt")
            reads(tmpch,tmpbuf)
            if (tmpbuf) then
                apiToken = %atrimtostring(tmpbuf)
            else
            begin
                ErrorMessage = "Failed to read API token from MailerSendApiToken.txt"
                ok = false
            end
        end
        catch (e, @Exception)
        begin
            ErrorMessage = "Failed to read API token from MailerSendApiToken.txt"
            ok = false
        end
        finally
        begin
            if (tmpch)
                close tmpch
        end
        endtry
    end

    ;Build the JSON message
    if (ok)
    begin
        data jsonBuffer = new StringBuilder()
        data jsonWriter = Utf8JsonWriter.CreateUtf8JsonWriter(jsonBuffer)
        data ix, int

        jsonWriter.WriteStartObject()

        jsonWriter.WriteStartObject("from")
        jsonWriter.WriteString("email",SenderAddress)
        jsonWriter.WriteString("name",SenderName)
        jsonWriter.WriteEndObject()

        jsonWriter.WriteStartArray("to")
        for ix from 1 thru RecipientNames.Length
        begin
            jsonWriter.WriteStartObject()
            jsonWriter.WriteString("email",RecipientAddresses[ix])
            jsonWriter.WriteString("name",RecipientNames[ix])
            jsonWriter.WriteEndObject()
        end
        jsonWriter.WriteEndArray()

        jsonWriter.WriteString("subject",Subject)
        jsonWriter.WriteString("text",BodyText)

        jsonWriter.WriteEndObject()
        jsonWriter.Flush()
        jsonWriter.Reset()

        requestBody = jsonBuffer.ToString()
    end

    ;Send the message to the MailerSend API
    if (ok)
    begin
        data status, int
        data responseBody, string
        data requestHeaders, [#]string
        data responseHeaders, [#]string

        requestHeaders = new string[3]
        requestHeaders[1] = "host: api.mailersend.com"
        requestHeaders[2] = "content-type: application/json"
        requestHeaders[3] = "authorization: Bearer " + apiToken

        status = %http_post("https://api.mailersend.com/v1/email",,requestBody,responseBody,ErrorMessage,requestHeaders,responseHeaders,"mailersend.log")

        using status select
        (0,200,201,202,204),
            nop
        (400,401,403,404,405,408,421,422,429,500,502,503,504),
        begin
            data jdoc = JsonDocument.Parse(responseBody)
            data rootElement = jdoc.RootElement
            data messageElement = rootElement.GetProperty("message")
            ErrorMessage = messageElement.GetString()
            ok = false
        end
        endusing
    end

    freturn ok

endfunction